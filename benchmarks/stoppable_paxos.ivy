#lang ivy1.6

# from Paxos Made EPR

################################################################################
#
# Modules that should probably come from a standard library
#
################################################################################

################################################################################
#
# Module for axiomatizing a total order
#
################################################################################

module total_order(r) = {
    axiom r(X,X)                        # Reflexivity
    axiom r(X, Y) & r(Y, Z) -> r(X, Z)  # Transitivity
    axiom r(X, Y) & r(Y, X) -> X = Y    # Anti-symmetry
    axiom r(X, Y) | r(Y, X)             # Totality
}


################################################################################
#
# Types, relations and functions describing state of the network
#
################################################################################

type votemap
type inst
type round
type value
type quorum
type node

individual none: round
relation le(X:round, Y:round)
instantiate total_order(le)

relation inst_le(X:inst, Y:inst)
instantiate total_order(inst_le)

# proposers can propose the stop value to stop the algorithm
individual stop: value

relation member(N:node, Q:quorum)
axiom forall Q1:quorum, Q2:quorum. exists N:node. member(N, Q1) & member(N, Q2)

relation one_a(R:round)
relation one_b_msg(N:node, R1:round, M:votemap)
relation left_round(N:node, R:round)
relation joined_round(N:node, R:round)
relation proposal(I:inst, R:round, V:value) # 2a
relation active(R:round) # round R has received a quorum of 1b and can now propose new values
relation vote(N:node, I:inst, R:round, V:value) # 2b
relation decision(N:node, I:inst, R:round, V:value) # got 2b from a quorum
function maxr(M:votemap,I:inst) : round
function maxv(M:votemap,I:inst) : value

init ~one_a(R)
init ~one_b_msg(N,R1,M)
init ~left_round(N,R)
init ~joined_round(N,R)
init ~proposal(I,R,V)
init ~active(R)
init ~vote(N,I,R,V)
init ~decision(N,I,R,V)


action send_1a = {
  local r:round {
    # a proposer selects a round and sends a message asking nodes to join the round
    assume r ~= none;
    one_a(r) := true
  }
}


action join_round = {
  local n:node, r:round, m:votemap {
    # receive 1a and answer with 1b
    assume r ~= none;
    assume one_a(r);
    assume ~left_round(n,r);

    # find the maximal vote in a round less than r, for all instances
    assume forall I:inst. (
        ( maxr(m,I) = none & forall R,V. ~(vote(n,I,R,V) & ~le(r,R)) ) |
        ( vote(n,I,maxr(m,I),maxv(m,I)) & ~le(r,maxr(m,I)) &
          forall R,V. vote(n,I,R,V) & ~le(r,R) -> le(R,maxr(m,I)) )
    );
    # send the 1b message
    one_b_msg(n,r,m) := true;
    # update code:
    left_round(n,R) := left_round(n,R) | ~le(r,R);
    joined_round(n,r) := true
  }
}


action receive_join_acks = {
  local r:round, q:quorum, m:votemap, im:inst {
    # receive a quorum of 1b's, activate round and propose for instances that have choosable values at lower rounds
    assume r ~= none;
    assume ~active(r); # this happens only once
    assume forall N:node. member(N, q) -> joined_round(N,r);

    # find the maximal vote in the quorum for each instance, according to votes themselves
    assume forall I:inst. (
        ( maxr(m,I) = none &
          forall N,R,V. ~(member(N, q) & ~le(r,R) & vote(N,I,R,V)) ) |
        ( (exists N,R,V.  member(N, q) & ~le(r,R) & vote(N,I,R,V) & R = maxr(m,I) & V = maxv(m,I)) &
          (forall N,R,V.  member(N, q) & ~le(r,R) & vote(N,I,R,V) -> le(R,maxr(m,I))) )
    );
    # activate round
    active(r) := true;
    # propose according to the overapproximating rule
    # check if there are stops
    if (forall I . ~(maxr(m,I) ~= none & maxv(m,I) = stop)) {
        # if there are no stops, propose according to m
        proposal(I, r, V) := proposal(I, r, V) | (maxr(m,I) ~= none & V = maxv(m,I))
    } else {
        # if there are stops, find the stop with the maximal round
        assume (
            maxr(m,im) ~= none & maxv(m,im) = stop &
            forall I . (maxr(m,I) ~= none & maxv(m,I) = stop) -> le(maxr(m,I),maxr(m,im))
        );
        # check if the stop at im is voided
        if (exists I . ~inst_le(I,im) & maxr(m,I) ~= none & ~le(maxr(m,I), maxr(m,im))) {
            # the stop at im is voided. void all stops, and also all
            # proposals at instances greater than im and lower
            # rounds. this is needed to be an overapproximation of
            # Lamport's version.
            proposal(I, r, V) := proposal(I, r, V) | (maxr(m,I) ~= none & V = maxv(m,I) &
                ~(maxv(m,I) = stop | (~inst_le(I,im) & ~le(maxr(m,im),maxr(m,I))))
            )
        } else {
            # the stop at im is not voided. void all other stops and
            # all proposals at instances larger than im
            proposal(I, r, V) := proposal(I, r, V) | (maxr(m,I) ~= none & V = maxv(m,I) &
                ~((I ~= im & maxv(m,I) = stop) | ~inst_le(I,im))
            )
        }
    }
  }
}


action propose = {
  local r:round, i:inst, v:value {
    # propose a new arbitrary value an available instance in an active round
    assume r ~= none;
    assume active(r);
    assume ~proposal(i, r, V);
    assume ~(~inst_le(i,I) & proposal(I, r, stop));
    assume ~(~inst_le(I,i) & proposal(I,r,V) & v = stop);
    proposal(i, r, v) := true
  }
}


action cast_vote = {
  local n:node, v:value, r:round, i:inst {
    # receive a 2a and send 2b
    assume r ~= none;
    assume ~left_round(n,r);
    assume proposal(i, r, v);
    vote(n, i, r, v) := true
  }
}


action decide = {
  local n:node, r:round, v:value, q:quorum, i:inst {
    # get 2b from a quorum
    assume r ~= none;
    assume member(N, q) -> vote(N, i, r, v);
    decision(n, i, r, v) := true
  }
}


export send_1a
export join_round
export receive_join_acks
export propose
export cast_vote
export decide


# safety property 1:
conjecture (
    decision(N1,I,R1,V1) &
    decision(N2,I,R2,V2)
) -> V1 = V2

# safety property 2:
conjecture decision(N1,I1,R1,stop) & ~inst_le(I2,I1) -> ~decision(N2,I2,R2,V)

template (forall M:votemap . forall I1:inst . forall I2:inst . forall A:node . forall C:value . forall D:value . forall Q:quorum . forall R1:round . forall R2:round . forall R3:round . WILD)

template (forall I1:inst . forall R1:round. forall R2:round, V:value. exists Q:quorum. forall M1:node, M2:node. WILD)

template (forall I1:inst, I2:inst . forall R1:round, R2:round, V1:value, V2:value, Q:quorum. exists N:node. WILD)
